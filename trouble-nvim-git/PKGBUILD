# Maintainer: Lanqing Yu <yulanqing126@gmail.com>
pkgname=trouble-nvim-git
pkgver=r137.897542f
pkgrel=1
pkgdesc="A pretty list for showing diagnostics, references, telescope results, quickfix and location lists to help you solve all the trouble your code is causing"
arch=('any')
url="https://github.com/folke/trouble.nvim"
license=('Apache')
depends=('neovim>=0.5.0')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
install=
source=("$pkgname::git+$url")
md5sums=('SKIP')

pkgver() {
	cd "$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$pkgname"
        find doc lua plugin -type f -exec install -Dm 644 '{}' "$pkgdir/usr/share/nvim/runtime/pack/dist/start/$pkgname/{}" \;
	install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
        install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
